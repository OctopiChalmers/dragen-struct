\section{Generating Random Constructions}
\label{sec:generators}


\begin{code}
type BGen a = Int -> Gen a

class BArbitrary (a :: *) where
  bounded :: BGen a
\end{code}


\begin{code}
class Arbitrary1 (f :: * -> *) where
  liftGen :: Gen a -> Gen (f a)
\end{code}

\begin{code}
class BArbitrary1 (f :: * -> *) where
  liftBGen :: BGen a -> BGen (f a)
\end{code}

\begin{code}
data ((f :: * -> *) otimes (n :: Nat)) a = Freq (f a)
\end{code}

\begin{code}
data Term (f :: * -> *) a = Term (f a)
\end{code}

\begin{code}
instance Algebra f a => Algebra (f otimes n) a where
  alg (Freq f) = alg f
\end{code}

\begin{code}
instance Algebra f a => Algebra (Term f) a where
  alg (Term f) = alg f
\end{code}


\begin{code}
instance BArbitrary1 f => BArbitrary1 (f otimes n) where
  liftBGen gen lvl = Freq <$> liftBGen gen lvl

instance BArbitrary1 f => BArbitrary1 (Term f) where
  liftBGen gen lvl = Term <$> liftBGen gen lvl
\end{code}

\begin{code}
type family FreqOf (f :: * -> *) :: Nat where
  FreqOf (f oplus g)   = FreqOf f + FreqOf g
  FreqOf (f otimes n)  = n * FreqOf f
  FreqOf (Term f)      = FreqOf f
  FreqOf _             = 1
\end{code}

\begin{code}
type family FreqOf' (f :: * -> *) :: Nat where
  FreqOf' (f oplus g)   = FreqOf' f  +  FreqOf' g
  FreqOf' (f otimes n)  = n  *  FreqOf' f
  FreqOf' (Term f)      = FreqOf f
  FreqOf' _             = 0
\end{code}


\begin{code}
instance (BArbitrary1 f, BArbitrary1 g)
  => BArbitrary1 (f oplus g) where
  liftBGen gen lvl =
    if lvl > 0
    then frequency
      [ (freqVal  at(FreqOf f) ,  InL <$> liftBGen gen lvl)
      , (freqVal  at(FreqOf g) ,  InR <$> liftBGen gen lvl) ]
    else frequency
      [ (freqVal  at(FreqOf' f) ,  InL <$> liftBGen gen lvl)
      , (freqVal  at(FreqOf' g) ,  InR <$> liftBGen gen lvl) ]
\end{code}


\begin{code}
instance BArbitrary1 Con_Text where
  liftBGen gen lvl = Con_Text <$> arbitrary

instance BArbitrary1 Con_Sing where
  liftBGen gen lvl = Con_Sing <$> arbitrary

instance BArbitrary1 Con_Tag where
  liftBGen gen lvl = Con_Tag <$> arbitrary <*> gen (lvl - 1)

instance BArbitrary1 Con_Join where
  liftBGen gen lvl = Con_Join <$> gen (lvl - 1) <*> gen (lvl - 1)
\end{code} %$


\begin{code}
instance BArbitrary1 Fun_br where
  liftBGen gen lvl = pure Fun_br

instance BArbitrary1 Fun_bold where
  liftBGen gen lvl = Fun_bold <$> gen (lvl - 1)

instance BArbitrary1 Fun_cat where
  liftBGen gen lvl = Fun_cat <$> gen (lvl - 1) <*> gen (lvl - 1)

instance BArbitrary1 Fun_list where
  liftBGen gen lvl = Fun_list <$> liftGen (gen (lvl - 1))
\end{code} %$

\begin{code}
instance BArbitrary1 Pat_simplify_1 where
  liftBGen gen lvl
    = Pat_simplify_1 <$> arbitrary <*> arbitrary

instance BArbitrary1 Pat_simplify_2 where
  liftBGen gen lvl
    = Pat_simplify_2 <$> gen (lvl - 1) <*> gen (lvl - 1)
\end{code}


\begin{code}
instance BArbitrary1 f => BArbitrary (Fix f) where
  bounded lvl = Fix <$> liftBGen bounded lvl
\end{code} %$

\begin{code}
genFix  ::  forall f dot BArbitrary1 f =>  BGen (Fix f)
genFix lvl = bounded at(Fix f) lvl
\end{code}

\begin{code}
genEval :: forall f a dot (BArbitrary1 f, Algebra f a) => BGen a
genEval lvl = eval <$> genFix at f lvl
\end{code} %$